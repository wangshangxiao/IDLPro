;计算波段反射率或辐亮度
FUNCTION REForRAD_CALCULATE,DN,SCALE,OFFSET
	REForRAD=(DN - OFFSET) * SCALE
	RETURN,REForRAD
END

;计算波段亮温
FUNCTION BT_CALCULATE,DN,SCALE,OFFSET,BMARK
	A=14387.9
	B=374151000.0
	RAD=REForRAD_CALCULATE(DN,SCALE,OFFSET)
	TEMP=READ_ASCII('data\paras\WL.txt')
	WL=TEMP.FIELD1[BMARK-1]
	RETURN,A / (WL * ALOG(B / (!PI * RAD* WL ^ 5) + 1))
END

;对各波段进行辐射定标
;由于13h和14h波段的存在，所以
;注意，BMARK从15波段开始，标记都要增加2，即15标记为17，16标记为18等
;13h标记为14，14标记为15，14h标记为16
FUNCTION MODIS_CALIBRATION,DN,SMARK,BMARK,REFLECTANCE = REFLECTANCE, RADIANCE = RADIANCE, BT = BT
	IF(SMARK EQ 'TER') THEN BEGIN
		SMARK = 'MOD'
	ENDIF ELSE BEGIN
		SMARK = 'MYD'
	ENDELSE
	IF(KEYWORD_SET(REFLECTANCE)) THEN BEGIN
		TEMP=READ_ASCII('data\paras\'+SMARK+'_Ref.txt')
		TEMP=TEMP.FIELD1
	ENDIF
	IF(KEYWORD_SET(RADIANCE) OR KEYWORD_SET(BT)) THEN BEGIN
		TEMP=READ_ASCII('data\paras\'+SMARK+'_Rad.txt')
		TEMP=TEMP.FIELD1
	ENDIF
	IF(KEYWORD_SET(REFLECTANCE) OR KEYWORD_SET(RADIANCE)) THEN BEGIN
		RETURN,REForRAD_CALCULATE(DN,TEMP[0,BMARK-1],TEMP[1,BMARK-1])
	ENDIF ELSE BEGIN
		RETURN,BT_CALCULATE(DN,TEMP[0,BMARK-1],TEMP[1,BMARK-1],BMARK)
	ENDELSE

END